import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:4.0'
        classpath 'org.yaml:snakeyaml:1.14'
        classpath 'com.github.michaelruocco:embedded-mysql-plugin:2.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'application'
apply plugin: 'embedded-mysql'

project.ext {
    artifactId = "dropwizard-hello-world"
    group = 'com.github.michaelruocco'
    version = '1.0.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.9.2'
    compile 'io.dropwizard:dropwizard-jdbi:0.9.2'

    compile 'org.jdbi:jdbi:2.73'
    compile 'com.h2database:h2:1.4.191'
    compile 'mysql:mysql-connector-java:5.1.37'
    compile 'org.flywaydb:flyway-core:4.0.1'
    compile 'io.federecio:dropwizard-swagger:0.7.0'

    testCompile 'junit:junit:4.11'
}

def getConfigPath() {
    'config/aat-web-template.yml'
}

def getDatabaseConfig() {
    def yaml = new Yaml()
    def config = new File(configPath)
    def yamlConfig = yaml.load(config.newReader())
    yamlConfig.database
}

embeddedMysql {
    def databaseConfig = getDatabaseConfig()
    url = databaseConfig.url
    version = 'v5_6_22'
}

flyway {
    def databaseConfig = getDatabaseConfig()
    user = databaseConfig.user
    password = databaseConfig.password
    url = databaseConfig.url
}

run {
    dependsOn startEmbeddedMysql, flywayMigrate, flywayInfo
    flywayMigrate.mustRunAfter startEmbeddedMysql
    flywayInfo.mustRunAfter flywayMigrate
    mainClassName = "uk.co.mruoc.Main"
    args 'server', configPath
}
