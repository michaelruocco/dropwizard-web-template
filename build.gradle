import org.yaml.snakeyaml.Yaml
import com.wiredforcode.gradle.spawn.SpawnProcessTask
import com.wiredforcode.gradle.spawn.KillProcessTask

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
        jcenter()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.26'
        classpath 'com.wiredforcode:gradle-spawn-plugin:0.8.1'
    }
}

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'com.github.michaelruocco.embedded-mysql-plugin' version '2.1.11'
    id 'org.flywaydb.flyway' version '6.5.0'
    id 'com.github.kt3k.coveralls' version '2.10.1'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.wiredforcode.spawn'
apply plugin: 'jacoco'

testSets {
    cucumberTest
}

configurations {
    cucumberTestRuntime {
        extendsFrom testImplementation
    }
}

project.ext {
    artifactId = "dropwizard-web-template"
    group = 'com.github.michaelruocco'
    version = '1.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:2.0.10'
    compile 'io.dropwizard:dropwizard-jdbi:2.0.0-rc9'
    compile 'io.dropwizard:dropwizard-testing:2.0.10'
    compile 'io.dropwizard:dropwizard-views-freemarker:2.0.10'
    compile 'uk.co.deloittedigital.dropwizard:dropwizard-hikaricp:1.0.0'
    compile 'org.jdbi:jdbi:2.78'
    compile 'mysql:mysql-connector-java:8.0.20'
    compile 'com.smoketurner:dropwizard-swagger:2.0.0-1'
    compile 'com.github.michaelruocco:http-client:4.0.4'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.yaml:snakeyaml:1.19'
    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'org.mockito:mockito-all:2.0.2-beta'

    testFixturesImplementation 'org.flywaydb:flyway-core:6.5.0'
    testFixturesImplementation 'commons-dbcp:commons-dbcp:1.4'

    cucumberTestCompile 'io.cucumber:cucumber-java:6.1.2'
    cucumberTestCompile 'org.flywaydb:flyway-core:6.5.0'
    cucumberTestCompile 'commons-dbcp:commons-dbcp:1.4'
}

def getConfigPath() {
    if (project.hasProperty("configPath"))
        return project.property("configPath")
    'config/local-web-template.yml'
}

def getDatabaseConfig() {
    def yaml = new Yaml()
    def config = new File(configPath)
    def yamlConfig = yaml.load(config.newReader())
    yamlConfig.database
}

embeddedMysql {
    def databaseConfig = getDatabaseConfig()
    url = databaseConfig.url
    version = 'v8_0_17'
}

flyway {
    def databaseConfig = getDatabaseConfig()

    user = databaseConfig.user
    password = databaseConfig.password
    url = databaseConfig.url
    driver = databaseConfig.driverClass
}

task setupMysql {
    dependsOn startEmbeddedMysql, flywayMigrate, flywayInfo
    flywayMigrate.mustRunAfter startEmbeddedMysql
    flywayInfo.mustRunAfter flywayMigrate
}

run {
    dependsOn setupMysql
    mainClassName = mainClassName
    if (project.hasProperty('fake.client.id')) {
        environment 'FAKE_CLIENT_ID', project.property('fake.client.id')
    }
    if (project.hasProperty('google.client.id')) {
        environment 'GOOGLE_CLIENT_ID', project.property('google.client.id')
    }
    if (project.hasProperty('google.client.secret')) {
        environment 'GOOGLE_CLIENT_SECRET', project.property('google.client.secret')
    }
    if (project.hasProperty('github.client.id')) {
        environment 'GITHUB_CLIENT_ID', project.property('github.client.id')
    }
    if (project.hasProperty('github.client.secret')) {
        environment 'GITHUB_CLIENT_SECRET', project.property('github.client.secret')
    }
    args 'server', configPath
    finalizedBy stopEmbeddedMysql
}

mainClassName = 'uk.co.mruoc.Main'

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task startServer(type: SpawnProcessTask) {
    dependsOn shadowJar, setupMysql
    setupMysql.mustRunAfter shadowJar
    command 'java -jar build/libs/' + project.ext.artifactId + '-all.jar server ' + configPath
    ready 'org.eclipse.jetty.server.Server: Started'
    pidLockFileName '/build/.bootRun.pid.lock'
}

task stopServer(type: KillProcessTask) {
    finalizedBy { stopEmbeddedMysql }
    pidLockFileName '/build/.bootRun.pid.lock'
}

task cucumber() {
    dependsOn assemble, cucumberTestClasses, startServer
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberTestRuntime + sourceSets.main.output + sourceSets.cucumberTest.output
            args = ['--plugin', 'pretty', '--glue', 'uk.co.mruoc', 'src/cucumberTest/resources']
        }
    }
    finalizedBy { stopServer }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}